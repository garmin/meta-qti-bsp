#@TYPE: Machine
#@NAME: sa6155
#@DESCRIPTION: Machine configuration for QTI sa6155

BASEMACHINE ?= "sa6155"
include conf/machine/include/auto-common.inc

# EMMC TARGET TO SUPPORT 64-bit kernel
DEFAULTTUNE = "aarch64"
MACHINE_FEATURES += "dt-overlay"

# Sizes for EXT4 (in bytes)
SYSTEM_SIZE_EXT4 ?= "100000000"
SYSTEMRW_SIZE_EXT4 ?= "8388608"
USERDATA_SIZE_EXT4 ?= "2000000000"
CACHE_SIZE_EXT4 ?= "80000000"

# Formats of root filesystem images.
IMAGE_FSTYPES += "ext4"
RECOVERY_IMAGE = "0"

KERNEL_BASE = "0x80000000"
KERNEL_TAGS_OFFSET = "0x1e00000"
KERNEL_CONFIG = "${@['vendor/sdmsteppe-auto-perf_defconfig', 'vendor/sdmsteppe-auto_defconfig'][d.getVar('VARIANT', True) == ('' or 'debug')]}"
KERNEL_CMD_PARAMS = "console=ttyMSM0,115200n8 earlycon=msm_geni_serial,0x880000 androidboot.hardware=qcom androidboot.console=ttyMSM0 androidboot.memcg=1 lpm_levels.sleep_disabled=1 video=vfb:640x400,bpp=32,memsize=3072000 msm_rtb.filter=0x237 service_locator.enable=1 androidboot.usbcontroller=a600000.dwc3 androidboot.selinux=permissive buildvariant=userdebug rootfstype=ext4 rw rootwait firmware_class.path=/firmware/image/ "
KERNEL_CMD_PARAMS_append = " fsck.repair=yes "
KERNEL_CMD_PARAMS_append = "${@bb.utils.contains_any('VARIANT', 'perf user', 'no_hotplug_area=0x80000000,0x40000000 dyn_memhotplug ', '', d)}"
KERNEL_CMD_PARAMS_append = "${@bb.utils.contains('DISTRO_FEATURES', 'early_init', 'early_userspace ', '', d)}"
KERNEL_CMD_PARAMS_remove = "${@bb.utils.contains_any('VARIANT', 'perf user', 'console=ttyMSM0,115200n8', '', d)}"
KERNEL_CMD_PARAMS_remove = "${@bb.utils.contains_any('VARIANT', 'perf user', 'earlycon=msm_geni_serial,0x880000', '', d)}"
KERNEL_CMD_PARAMS_remove = "${@bb.utils.contains_any('VARIANT', 'perf user', 'androidboot.console=ttyMSM0', '', d)}"
# Workaround for current serial driver
KERNEL_CMD_PARAMS_append = "${@bb.utils.contains('VARIANT', 'debug', ' quiet systemd.log_target=null ', '', d)}"
KERNEL_CMD_PARAMS_remove = "${@bb.utils.contains('IMAGE_FEATURES', 'read-only-rootfs', 'rw', '', d)}"
KERNEL_CMD_PARAMS_append = " androidboot.android_dt_dir=/skip/ androidboot.console=tty30 "
KERNEL_IMAGETYPE = "Image-dtb"

IMAGE_FEATURES_append = " read-only-rootfs"

MACHINE_FEATURES += "qti-core qti-audio qti-bluetooth qti-data qti-display qti-extra qti-multimedia qti-camera qti-graphics qti-security qti-vnw qti-wlan qti-tools"
